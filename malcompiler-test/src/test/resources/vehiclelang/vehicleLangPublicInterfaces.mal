category System {

    asset InfotainmentSystem extends Machine
        user info: "Represents the information & entertainment system found on all modern cars. It is a machine which can be connected to one or more networks."
        developer info: "It has the same functionality as a machine plus one additional attack step that is reached only when the NetworkAccessService is compromised."
        {
        | access
                developer info: "Adding one new connected attack step"
                +>  engineerNetworkAccess

        | gainNetworkAccess
                user info: "If this attack step is reached then full network layer access is gained by the attacker."
                ->	connectedNetworks.accessNetworkLayer

        | engineerNetworkAccess [Exponential(10.0)]
                user info: "This attack step is another way to reach full network access if there is no network access service on the infotainment system, but it requires effort!"
                ->	connectedNetworks.accessNetworkLayer
        }

    asset NetworkAccessService extends NetworkService
        user info: "This service might run on an infotainment system and if compromised allows the attacker to access the networks connected to it."
        {
        | access
                +>	executor.gainNetworkAccess
        }
}

category Communication {

    asset OBD2Connector
        user info: "Represents the OBD-II connector available in all modern cars and most vehicles in general."
        {
        | physicalAccess
                user info: "Physical access to the connector leads to access on the network layer."
                ->	interfacingNetworks.accessNetworkLayer

        | connect
                -> bypassConnectorProtection,
                _connectNoProtection

        | bypassConnectorProtection [Exponential(20.0)]
                user info: "Remove or bypass objects blocking the OBD connector, for example ripping of protective plate or ganining access to driver cabin. Requires effort"
                ->	physicalAccess

        & _connectNoProtection
                -> 	physicalAccess

        # connectorAccessProtection
                user info: "Any type of physical entity blocking attackers from physically connecting to the OBD-II port. For example a protective plate covering the port or port being placed where it's difficult to access."
                ->	_connectNoProtection
        }

    asset ChargingPlugConnector
        user info: "The charging plug on many electric vehicles provides direct CAN bus access, while on others is only connected to the same network as the Battery Management System (BMS) ECU."
        developer info: "Florian Sagstetter, Security Challenges in Automotive Hardware/Software Architecture Design (2013)"
        {
        | physicalAccess
                user info: "No matter the case, physical access to the connector leads to access on the network layer of the connected network."
                ->	connectedNetwork.accessNetworkLayer
        }

    asset AftermarketDongle
        user info: "An aftermarket dongle is a device that connects to the OBD-II port and provides some additional functionality to the vehicle's owner (e.g. error log reading, vehicle configuration, etc."
        {
        | connectDongle
                user info: "When a dongle is connected, the connect attack step is reached on OBD-II connector."
                ->	_connectToNetwork

        & _connectToNetwork
                ->	connector.connect

        # dongleIsHardened
                user info: "If the firmware on the connected dongle cannot be modified by the attacker, then access on the network layer cannto be achieved."
                developer info: "This defense might look more logical in the future where an external attacker will be able to use the dongle as an entry point in the vehicle."
                ->	_connectToNetwork
        }
}

associations {
    VehicleNetwork		[interfacingNetworks]		*	<-- Interface						-->	0..1[connector]						OBD2Connector
    VehicleNetwork		[connectedNetwork]			0..1<--	Connections						--> 0..1[chargingPlug]					ChargingPlugConnector
    OBD2Connector		[connector]					0..1<--	Connection						--> 0..1[dongle]						AftermarketDongle
    Network 			[connectedNetworks] 		* 	<-- Connection 						--> * 	[infotainment] 					InfotainmentSystem
}
